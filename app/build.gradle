plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

apply from: "../versions.gradle"

android {
    compileSdkVersion versions.compileSdk

    defaultConfig {
        applicationId "com.nedaluof.quotes"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.compileSdk
        versionCode versions.versionCode
        versionName versions.versionName
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def buildType = variant.buildType.name
            def version = variant.versionName
            def formattedDate = new Date().format("dd-MM-yyyy hh-mm a")
            def newApkName = "Quotes($buildType-$version-$formattedDate).apk"
            output.outputFileName = new File(buildType == "debug" ? "debug" : "release", newApkName)
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    kapt { correctErrorTypes = true }

    buildFeatures.dataBinding = true
}

dependencies {
    //domain
    api project(":domain")
    //app compatibility
    implementation "androidx.appcompat:appcompat:$versions.appcompat"
    //MDC
    implementation "com.google.android.material:material:$versions.material"
    //constraint layout
    implementation "androidx.constraintlayout:constraintlayout:$versions.constraintlayout"
    //RecyclerView
    implementation "androidx.recyclerview:recyclerview:$versions.recyclerView"
    //shimmer effect
    implementation "com.facebook.shimmer:shimmer:$versions.shimmer"
    //bouncy nested scroll view
    implementation "com.factor:bouncy:$versions.bouncy"
    //design dimensions helper
    implementation "com.intuit.sdp:sdp-android:$versions.intuit"
    implementation "com.intuit.ssp:ssp-android:$versions.intuit"
    //extensions
    implementation "androidx.fragment:fragment-ktx:$versions.fragmentKtx"
    // jet-pack Components
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.lifecycle"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$versions.lifecycle"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$versions.lifecycle"
    // Hilt dependencies injection
    implementation "com.google.dagger:hilt-android:$versions.hilt"
    kapt "com.google.dagger:hilt-android-compiler:$versions.hilt"
    //kotlin reflect
    implementation "org.jetbrains.kotlin:kotlin-reflect:$versions.reflect"
    // multidex
    implementation "androidx.multidex:multidex:$versions.multidex"
    //java 8+ support
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:$versions.java8"
    //testing
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}